plugins {
    alias libs.plugins.kotlin apply false
    alias libs.plugins.android.library apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext.androidSdkVersion = 33

ext.applyCommonAndroidParameters = { project ->
    def android = project.android
    android.compileSdk androidSdkVersion
    android.compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        incremental = true
    }
    android.defaultConfig {
        minSdkVersion 24
        targetSdkVersion androidSdkVersion
    }
}

ext.applyLibraryModuleOptimisations = { project ->
    project.android {
        variantFilter { variant ->
            if (variant.name == "debug") {
                variant.ignore = true
            }
        }

        buildFeatures {
            buildConfig = false
            dataBinding = false
            aidl = false
            renderScript = false
            resValues = false
            shaders = false
            viewBinding = false
        }
    }
}

ext.applyCompose = { project ->
    def dependencies = project.dependencies

    dependencies.implementation libs.compose.ui
    dependencies.implementation libs.compose.foundation
    dependencies.implementation libs.compose.activity

    def android = project.android
    android.buildFeatures.compose = true
    android.composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.compose.compiler.get()
    }
}

ext.applyAndroidComposeLibraryModule = { project ->
    applyAndroidLibraryModule(project)
    applyCompose(project)
}

ext.applyAndroidLibraryModule = { project ->
    applyCommonAndroidParameters(project)
    applyLibraryModuleOptimisations(project)
}

ext.kotlinTest = { dependencies ->
    dependencies.testImplementation libs.kluent
    dependencies.testImplementation libs.kotlinTest
    dependencies.testImplementation libs.mockk
    dependencies.testImplementation libs.kotlin.coroutines.test
}

ext.kotlinFixtures = { dependencies ->
    dependencies.testFixturesImplementation libs.mockk
    dependencies.testFixturesImplementation libs.kluent
    dependencies.testFixturesImplementation libs.kotlin.coroutines
}
